apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 35

    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.todoapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'

        multiDexEnabled = true
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    namespace 'com.example.android.architecture.blueprints.todoapp'
}

dependencies {
    // Core dependencies
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Room
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation 'androidx.compose.ui:ui-test-android:1.7.6'
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"

    // Test dependencies
    androidTestImplementation "androidx.test:runner:$rootProject.runnerVersionAndroidX"
    androidTestImplementation "androidx.test:rules:$rootProject.rulesVersionAndroidX"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.jUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$rootProject.espressoVersionAndroidX"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'

    // Test orchestrator
    androidTestUtil "androidx.test:orchestrator:1.5.1"

    // Additional dependencies
    implementation "com.squareup.picasso:picasso:2.71828"
    androidTestImplementation "com.azimolabs.conditionwatcher:conditionwatcher:0.2"
}

repositories {
    mavenCentral()
    google()
}